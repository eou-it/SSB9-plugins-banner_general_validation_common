/** *****************************************************************************
 © 2010 SunGard Higher Education.  All Rights Reserved.

 CONFIDENTIAL BUSINESS INFORMATION

 THIS PROGRAM IS PROPRIETARY INFORMATION OF SUNGARD HIGHER EDUCATION
 AND IS NOT TO BE COPIED, REPRODUCED, LENT, OR DISPOSED OF,
 NOR USED FOR ANY PURPOSE OTHER THAN THAT WHICH IT IS SPECIFICALLY PROVIDED
 WITHOUT THE WRITTEN PERMISSION OF THE SAID COMPANY
 ****************************************************************************** */
package com.sungardhe.banner.general.system

import com.sungardhe.banner.json.JsonHelper
import com.sungardhe.banner.supplemental.SupplementalPropertyDiscriminatorContent
import com.sungardhe.banner.supplemental.SupplementalPropertyValue
import com.sungardhe.banner.testing.BaseIntegrationTestCase

import grails.converters.JSON

import groovy.sql.Sql

import java.sql.Connection

import org.codehaus.groovy.grails.commons.ConfigurationHolder
import org.springframework.security.core.context.SecurityContextHolder

/**
 * Integration test for the College controller.
 **/
class CollegeControllerIntegrationTests extends BaseIntegrationTestCase {

    def collegeService                      // injected by Spring
    def lookupService                       // injected by Spring
    def supplementalDataService             // injected by Spring
    def supplementalDataPersistenceManager  // injected by Spring


    static List serviceCallbacks


    protected void setUp() {

        // For testing RESTful APIs, we don't want the default 'controller support' added by our base class.
        // Most importantly, we don't want to redefine the controller's params to be a map within this test,
        // as we need Grails to automatically populate the params from the request.
        //
        // So, we'll set the formContext and then call super(), just as if this were not a controller test.
        // That is, we'll set the controller after we call super() so the base class won't manipulate it.
        formContext = [ 'STVCOLL' ]

        assert collegeService != null
        assert lookupService != null

        controller = new CollegeController()
        controller.collegeService = collegeService
        controller.lookupService = lookupService
        super.setUp()
    }

    void testShow_Json() {

        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity

        controller.request.with {
            method = 'GET'
            content = "{'id': ${entity.id} }".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.show()

        def result = JSON.parse( controller.response.contentAsString )
        JsonHelper.replaceJSONObjectNULL( result ) // Minimal workaround for Jira Grails-5585; TODO: Remove when Jira is resolved.
        assertEquals "Found id ${result?.data?.id} but expected ${entity.id}", entity.id, result?.data?.id
        assertEquals "Found code ${result?.data?.code} but expected ${entity.code}", entity.code, result?.data?.code
        assertEquals "Found description ${result?.data?.description} but expected ${entity.description}", entity.description, result?.data?.description
        assertNull result?.supplementalData
    }


    void testShowWithSupplementalData_Json() {
        try {
            supplementalDataService.appendSupplementalDataConfiguration( [ "com.sungardhe.banner.general.system.College":
                                                                                [ testSuppA: [ required: false, dataType: String ],
                                                                                  testSuppB: [ required: false, dataType: boolean ]
                                                                                ]
                                                                          ] )

            supplementalDataService.supplementalDataPersistenceManager = new SupplementalDataPersistenceTestManager()

            def entity = new College( newCollegeParamsWithAuditTrailProperties() )
            save entity
            entity.refresh()

            // Note that we do NOT support using supplemental property setters on model instances that have not yet been persisted to the database
            entity.testSuppA = new SupplementalPropertyDiscriminatorContent( value: "Supplemental property A" )
            entity.testSuppA.'2' = [ value: "Supp A too!", disc: 2, required: false, pkParentTab: null, id: 42, dataType: String ]
            save entity

            controller.request.with {
                method = 'GET'
                content = "{'id': ${entity.id} }".getBytes()
                contentType = "application/json"
                getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
            }
            controller.show()

            def result = JSON.parse( controller.response.contentAsString )
            JsonHelper.replaceJSONObjectNULL( result?.data ) // Minimal workaround for Jira Grails-5585; TODO: Remove when Jira is resolved.
            assertEquals "Found id ${result?.data?.id} but expected ${entity.id}", entity.id, result?.data?.id
            assertEquals "Found code ${result?.data?.code} but expected ${entity.code}", entity.code, result?.data?.code
            assertEquals "Found description ${result?.data?.description} but expected ${entity.description}", entity.description, result?.data?.description
            assertEquals "Supplemental property A", result?.supplementalData?.testSuppA.'1'?.value
            assertEquals "Supp A too!", result?.supplementalData?.testSuppA?.'2'?.value
        } finally {
            supplementalDataService.supplementalDataConfiguration.remove( "com.sungardhe.banner.general.system.College" )
     	    supplementalDataService.supplementalDataPersistenceManager = supplementalDataPersistenceManager
         }
    }


    void testList_Json() {

        def MAX = 15
        controller.request.with {
            method = 'GET'
            content = "{'max': ${MAX} }".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.list()

        def result = JSON.parse( controller.response.contentAsString )
        assertTrue "Found ${result?.data?.size()} but expected $MAX", result?.data?.size() == MAX
        assertTrue "Total count expected to be ${College.count()} but was ${result?.totalCount}", result?.totalCount == College.count()

        // Next we'll reconstitute College model instances from the JSON
        // The following fails when 'unit' tests are run alongside of integration tests. The exception is a 'null' violation
        // due to the lastModified date not being set. The underlying exception is a JSON converter exception due to not being
        // able to parse the date format. The Config.groovy sets grails.converters.json.date = "javascript", however when
        // unit tests are run this setting is apparently not effective.
        //
        // For now, this isn't integral to the framework plugin testing, so we'll comment out so all tests can run together.
        // This test exists within banner_on_grails (e.g., CollegeControllerIntegrationTests) which forces unit and integration tests to be
        // executed separately.
        result.data.each {
            JsonHelper.replaceJSONObjectNULL( it ) // Minimal workaround for Jira Grails-5585
            def college = new College( it )
//            if (!college.validate()) {
//                def message
//                college.errors.allErrors.each {
//                  message += "${it}\n"
//                }
//                fail( "Validation errors: $message" )
//            }
        }
    }


    void testInsert_Json() {

        controller.request.with {
            method = 'POST'
            content = "${newCollegeParams() as JSON}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.create()

        def content = controller?.response?.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertTrue result?.success

        assertNotNull result?.data?.id
        assertEquals 'TT', result?.data?.code

        assertEquals ConfigurationHolder.config?.dataOrigin, result?.data?.dataOrigin
        assertEquals SecurityContextHolder.context?.authentication?.principal?.username, result?.data?.lastModifiedBy
        assertNotNull result?.data?.lastModified
    }


    void testAttemptInsertInvalidEntity_Json() {

        def paramMap = newCollegeParams()
        paramMap.code = "TOO_LONG"

        controller.request.with {
            method = 'POST'
            content = "${paramMap as JSON}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
            contentType = "text/json"
        }
        controller.create()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertFalse result?.success

        assertTrue "Message was not expected: ${result?.message}",
                    result?.message ==~ /.*College cannot be saved, as it contains errors.*/
        assertTrue "Errors were not expected: ${result?.errors}",
                    result?.errors ==~ /.*The college code is too long, it must be no more than 2 characters.*/
    }


    void testUpdate_Json() {

        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity
        int version = entity.version
        def code = "JJ"

        controller.request.with {
            method = 'PUT'
            content = "${((newCollegeParams() + [ id: entity.id, version: entity.version, code: code ]) as JSON)}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.update()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertTrue result?.success
        assertNotNull result?.message // TODO: assert correct localized message

        entity = College.get( entity.id )
        assertEquals code, entity.code
        assertEquals version + 1, entity.version
    }


    void testUpdateNotFound_Json() {

        controller.request.with {
            method = 'PUT'
            content = "${((newCollegeParams() + [ id: -66666 ]) as JSON)}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.update()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertFalse result?.success
        assertTrue "Message was not expected: ${result?.message}", result?.message ==~ /.*not found.*/
    }


    void testOptimisticLock_Json() {

        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity
        int version = entity.version

        // Now we'll issue a SQL update statement to adjust the version column
        executeUpdateSQL "update STVCOLL set STVCOLL_VERSION = 999 where STVCOLL_SURROGATE_ID = ?", entity.id

        def description = "This better fail" // due to optimistic lock exception

        controller.request.with {
            method = 'PUT'
            content = "${(newCollegeParams() + [ id: entity.id, version: entity.version, description: description ]) as JSON}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.update()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertFalse result?.success

        JsonHelper.replaceJSONObjectNULL( result ) // Minimal workaround for Jira Grails-5585
        assertNull result?.errors

        assertTrue "Message was not expected: ${result?.message}",
                   result?.message ==~ /.*Another user has updated this College while you were editing.*/

        assertTrue "Errors were not expected: ${result?.internalError}",
                   result?.underlyingErrorMessage ==~ /.*optimistic locking failed.*/ // underlying hibernate error message, not presentable to user...
    }


    void testOptimisticLockFailuresTrumpValidationFailures_Json() {
        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity
        int version = entity.version

        // Now we'll issue a SQL update statement to adjust the version column
        executeUpdateSQL "update STVCOLL set STVCOLL_VERSION = 999 where STVCOLL_SURROGATE_ID = ?", entity.id

        controller.request.with {
            method = 'PUT'
            content = "${[ id: entity.id, code: '', version: entity.version ] as JSON}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        // Should fail with optimistic lock even though it would also fail validation due to code being set to null
        controller.update()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertFalse result?.success

        JsonHelper.replaceJSONObjectNULL( result ) // Minimal workaround for Jira Grails-5585
        assertNull result?.errors

        assertTrue "Message was not expected: ${result?.message}",
                   result?.message ==~ /.*Another user has updated this College while you were editing.*/
        assertTrue "Errors were not expected: ${result?.internalError}",
                   result?.underlyingErrorMessage ==~ /.*optimistic locking failed.*/ // underlying hibernate error message, TODO: Make presentable to user...
    }


    void testUpdateOnlyWhenDirty_Json() {

        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity

        int version = entity.version
        def lastModified = entity.lastModified

        controller.request.with {
            method = 'PUT'
            content = "${((newCollegeParams() + [ id: entity.id, version: entity.version ]) as JSON)}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.update()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertTrue result?.success
        assertNotNull result?.message // TODO: assert correct localized message

        entity = College.get( entity.id )
        assertEquals version, entity.version // should not be incremented!
        assertEquals lastModified, entity.lastModified
    }


    void testDelete_Json() {

        def entity = new College( newCollegeParamsWithAuditTrailProperties() )
        save entity

        controller.request.with {
            method = 'DELETE'
            content = "${((newCollegeParams() + [ id: entity.id, version: entity.version ]) as JSON)}".getBytes()
            contentType = "application/json"
            getAttribute( "org.codehaus.groovy.grails.WEB_REQUEST" ).informParameterCreationListeners()
        }
        controller.destroy()

        def content = controller.response.contentAsString
        assertNotNull content

        def result = JSON.parse( content )
        assertTrue "Expected success to be true but found results = $result", result?.success
        assertNotNull result?.message // TODO: assert correct localized message

        assertNull College.get( entity.id )
    }


    // DO NOT COPY THIS TEST TO OTHER CONTROLLER TESTS.
    // This is not really a 'controller' test, this tests that we can see pending creates in the
    // current transaction and that we cannot see pending creates in a different transaction.
    // We'll put this here as we need the full database environment versus a unit test.
    void testJoiningTransaction() {
        Connection conn = null
        def sql = null
        def found = "I better be set to null by otherThread!"
        def otherThread

        try {
            College.withTransaction {
                def college = new College( newCollegeParamsWithAuditTrailProperties() ).save( flush: true )

                // Now try from a connection in the same thread...
                conn = sessionFactory.getCurrentSession().connection()
                sql = new Sql( conn )
                def row = sql.firstRow("select STVCOLL_CODE from saturn.STVCOLL where STVCOLL_CODE = 'TT'")
                assertEquals("Expected code 'TT' but found code ${row?.STVCOLL_CODE}", "TT", row?.STVCOLL_CODE)

                // and lastly ensure we can't see it using a connnection from another thread...
                otherThread = new Thread( {  // we'll use a closure to get a connection via a different thread
                    com.sungardhe.banner.security.FormContext.set( ['STVCOLL'] )
                    login()
                    def sqlB = null
                    try {
                        sqlB = new Sql( dataSource )
                        found = sqlB.firstRow( "select STVCOLL_CODE from saturn.STVCOLL where STVCOLL_CODE = 'TT'" )
                    } finally {
                        sqlB?.close()
                    }
                })
                otherThread.start()
            }
        } finally {
            sql?.close()
            // note we don't close the connection - the test framework will perform a rollback and then close
        }

        otherThread.join()
        assertNull( found )
    }


    // NOTICE: A 'functional' test is used to test the XML interface (as it relies on GSP processing that is beyond the scope of an integration test)


    // NOTICE: Depends on seed data having pidm 1627 and having at least one medical condition and disability record each with surrogate key 1
    // Creates params appropriate for the controller
    private Map newCollegeParams() {
        [ code: "TT", description: "TT", addressStreetLine1: "TT", addressStreetLine2: "TT", addressStreetLine3: "TT", addressCity: "TT",
		  addressState: "TT", addressCountry: "TT", addressZipCode: "TT", systemRequiredIndicator: "N", voiceResponseMessageNumber: 1, statisticsCanadianInstitution: "TT",
		  districtDivision: "TT", houseNumber: "TT", addressStreetLine4: "TT" ]
    }


    // Includes all properties so that the college is valid and can be saved directly via GORM versus going through the service
    private Map newCollegeParamsWithAuditTrailProperties() {
        (newCollegeParams() + [ lastModified: new Date(), lastModifiedBy: 'horizon', dataOrigin: 'horizon' ])
    }

}


class SupplementalDataPersistenceTestManager {


    // Map contains: [ modelKey: [ propertyName: supplementalPropertyValueInstance ] ]
    Map<String,Map<String,SupplementalPropertyValue>> persistentStore  = new HashMap()


    public def loadSupplementalDataFor( model ) {
        println "SupplementalDataPersistenceTestManager.load called for model $model"
        def modelKey = "${model.class.name}-${model.id}"
        if (persistentStore."$modelKey") {
            model.supplementalProperties = persistentStore."$modelKey".clone()
        }
        else {
            println "...and will load default definitions (since there is no existing supplemental data"
            // load in common specification of the supplemental data properties
            Map defaultSuppData = [ testSuppA: newDefaultPropertySpec( '1' ),
                                    testSuppB: newDefaultPropertySpec( '1', Boolean ) ]
            model.supplementalProperties = defaultSuppData
        }
    }


    public def persistSupplementalDataFor( model ) {
        println "SupplementalDataPersistenceTestManager.persist called"
        def modelKey = "${model.class.name}-${model.id}"
        persistentStore."$modelKey" = new HashMap()

        model.supplementalProperties?.each { k, v ->
            persistentStore."$modelKey".put( k, v )
        }
    }


    public def removeSupplementalDataFor( model ) {
        // not needed in this test implementation
    }


    private SupplementalPropertyValue newDefaultPropertySpec( discriminator = '1', dataType = String ) {
        SupplementalPropertyDiscriminatorContent discProp =
            new SupplementalPropertyDiscriminatorContent( disc: discriminator, dataType: dataType )

        new SupplementalPropertyValue( [ (discProp.disc): discProp ] )
    }

}
